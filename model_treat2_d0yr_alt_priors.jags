model {
  
  ### prior distributions ###
  
  # parameter correlation
  
  rho.ab ~ dunif(-1,1)
  rho.bd ~ dunif(-1,1)
  
  # mean and precison for the parameter intercepts

  alpha0 ~  dnorm(0, 0.01)
  sigma.a0 ~ dunif(0, 10)

  beta0 ~ dnorm(0, 0.01)
  sigma.b0 ~ dunif(0, 10)
  
  for(t in 1:n.year) {
    delta0[t] ~ dnorm(0, 0.01)
    sigma.d0[t] ~ dunif(0, 10)
  }
  delta0.mean <- sum(delta0[])/n.year
  sigma.d0.mean <- sum(sigma.d0[])/n.year
  
  # metacommunity parameter
  omega ~ dunif(0,1)
  
  # covariates for psi (d)
  Betad.PctTrt ~ dnorm(0, 0.01)
  sigma.Betad.PctTrt ~ dunif(0, 10)

  Betad.PctTrt2 ~ dnorm(0, 0.01)
  sigma.Betad.PctTrt2 ~ dunif(0, 10)

  Betad.TWIP ~ dnorm(0, 0.01)
  sigma.Betad.TWIP ~ dunif(0, 10)
  
  Betad.Rdens ~ dnorm(0, 0.01)
  sigma.Betad.Rdens ~ dunif(0, 10)

  # covariates for theta (b)
  Betab.Trt ~ dnorm(0, 0.01)
  sigma.Betab.Trt ~ dunif(0, 10)

  Betab.YST ~ dnorm(0, 0.01)
  sigma.Betab.YST ~ dunif(0, 10)

  Betab.TWIP ~ dnorm(0, 0.01)
  sigma.Betab.TWIP ~ dunif(0, 10)
  
  # covariates for p (a)
  Betaa.Time ~ dnorm(0, 0.01)
  sigma.Betaa.Time ~ dunif(0, 10)

  Betaa.Time2 ~ dnorm(0, 0.01) # Possibly drop quadratics here.
  sigma.Betaa.Time2 ~ dunif(0, 10)

  Betaa.DOY ~ dnorm(0, 0.01)
  sigma.Betaa.DOY ~ dunif(0, 10)

  Betaa.Trt ~ dnorm(0, 0.01)
  sigma.Betaa.Trt ~ dunif(0, 10)

  Betaa.YST ~ dnorm(0, 0.01)
  sigma.Betaa.YST ~ dunif(0, 10)

  for (i in 1:n.spp) {
    for(t in 1:n.year) {
      d0[i, t] ~ dnorm(delta0[t], pow(sigma.d0[t], -2))
    }
    d0.mean[i] <- sum(d0[i,])/n.year
    b0[i] ~ dnorm(beta0 + (rho.bd*sigma.b0/sigma.d0.mean)*(d0.mean[i] -
      delta0.mean),  pow(sigma.b0,-2)/(1 - pow(rho.bd,2)))
    a0[i] ~ dnorm(alpha0 + (rho.ab*sigma.a0/sigma.b0)*(b0[i] - beta0),  pow(sigma.a0,-2)/(1 - pow(rho.ab,2)))

    # covariates for psi (d)
    bd.ptrt[i] ~ dnorm(Betad.PctTrt, pow(sigma.Betad.PctTrt, -2))
    bd.ptrt2[i] ~ dnorm(Betad.PctTrt2, pow(sigma.Betad.PctTrt2, -2))
    bd.TWIP[i] ~ dnorm(Betad.TWIP, pow(sigma.Betad.TWIP, -2))
    bd.Rdens[i] ~ dnorm(Betad.Rdens, pow(sigma.Betad.Rdens, -2))

    # covariates for theta (b)
    bb.trt[i] ~ dnorm(Betab.Trt, pow(sigma.Betab.Trt, -2))
    bb.YST[i] ~ dnorm(Betab.YST, pow(sigma.Betab.YST, -2))

    # covariates for p (a)
    ba.Time[i] ~ dnorm(Betaa.Time, pow(sigma.Betaa.Time, -2))
    ba.Time2[i] ~ dnorm(Betaa.Time2, pow(sigma.Betaa.Time2, -2))
    ba.DOY[i] ~ dnorm(Betaa.DOY, pow(sigma.Betaa.DOY, -2))
    ba.trt[i] ~ dnorm(Betaa.Trt, pow(sigma.Betaa.Trt, -2))
    ba.YST[i] ~ dnorm(Betaa.YST, pow(sigma.Betaa.YST, -2))

    # state processes
    w[i] ~ dbern(omega)
    for(k in 1:n.grid) {
      for(t in 1:n.year) {
        logit(psi[k, i, t]) <- d0[i, t] + bd.ptrt[i]*PctTrt.d[k, t] + bd.ptrt2[i]*pow(PctTrt.d[k, t], 2) +
          bd.TWIP[i]*TWIP.d[k, t] + bd.Rdens[i]*Rdens.d[k, t]
        z[k, i, t] ~ dbern(psi[k, i, t]*w[i])
      }
    }

    for(j in 1:n.point_year) {
      logit(theta[j, i]) <- b0[i] + bb.trt[i]*Trt.b[j] + bb.YST[i]*YST.b[j]
        u[j, i] ~ dbern(theta[j, i]*z[gridID[j], i, yearID[j]])
      
      logit(p[j, i]) <- a0[i] + ba.Time[i]*Time.b[j] + ba.Time2[i]*pow(Time.b[j], 2) +
        ba.DOY[i]*DOY.b[j] + #ba.DOY2[i]*pow(DOY.b[j], 2) +
        ba.trt[i]*Trt.b[j]*Trt.b[j] + ba.YST[i]*YST.b[j]
      Y[j, i] ~ dbinom(p[j, i]*u[j, i], TPeriod[j, i])
    }
  }
  for(k in 1:n.grid) {
    for(t in 1:n.year) {
      SR.grid[k, t] <- sum(z[k, , t])
    }
  }
  for(j in 1:n.point_year) {
    SR.point[j] <- sum(u[j, ])
  }
}
